//a  - FUNCIONA
CALL{
    MATCH (i:INDIVIDUAL)-[v:VIU]->()
    WHERE v.Location="SFLL" AND v.Year IS NOT NULL
    WITH v.Year as Year, 
        size(collect(i)) AS Habitants, 
        collect(DISTINCT i.surname) + collect(DISTINCT i.second_surname) AS CognomsRepetits UNWIND CognomsRepetits as Surnames 
    RETURN Year, Habitants, collect(DISTINCT Surnames) as Cognoms
    ORDER BY Year
}
WITH Year, Habitants, [cognom in Cognoms WHERE cognom <> "nan"] as CognomsFiltrats
RETURN Year, Habitants, CognomsFiltrats


//b - FUNCIONA
CALL{
    CALL db.index.fulltext.queryNodes('IndividuNomCognoms', 'miguel estape bofill')
    YIELD node, score
    RETURN node
    LIMIT 1
} 
MATCH (i:INDIVIDUAL)-[:SAME_AS]->(i2)
WHERE i.name=node.name AND i.surname=node.surname AND i.second_surname=node.second_surname
RETURN i.name as Nom, collect(DISTINCT i2.surname) as PrimerCognom, collect(DISTINCT i2.second_surname) as SegonCognom

//c - FUNCIONA
CALL{
    CALL db.index.fulltext.queryNodes('IndividuNomCognoms', 'benito julivert')
    YIELD node, score
    RETURN node
    LIMIT 1
} 
MATCH (n:INDIVIDUAL)-[f:FAMILIA]-(m:INDIVIDUAL) 
WHERE n = node AND f.Relacio_Harmonitzada IN ["fill", "filla"]
WITH m.name as Nom, m.surname as Cognom, m.second_surname as SegonCognom, f.Relacio_Harmonitzada as Relacio
RETURN Nom, Cognom, SegonCognom, Relacio
ORDER BY Nom

//d - FUNCIONA
MATCH (:HABITATGES)<-[v:VIU {Location: "CR"}]-(i:INDIVIDUAL)<-[f:FAMILIA]-(:INDIVIDUAL)
WHERE f.Relacio_Harmonitzada IN ["fill", "filla"]
WITH i.name as Nom, i.surname as Cognom, i.second_surname as SegonCognom, count(f) as numFills
WHERE numFills>3
RETURN Nom, Cognom, SegonCognom, numFills
ORDER BY numFills DESC
LIMIT 20

//e - FUNCIONA
CALL{
    MATCH (i:INDIVIDUAL)-[v:VIU]->(h:HABITATGES)
    WHERE v.Location="SFLL"
    WITH v.Year as Any_Padro, h.Carrer as Carrer, count(i) as Habitants
    RETURN Any_Padro,  min(Habitants) as minHabitants
}
MATCH (i:INDIVIDUAL)-[v:VIU]->(h:HABITATGES)
WHERE v.Location="SFLL"
WITH v.Year as Padro, h.Carrer as Carrer, count(i) as Habitants, minHabitants, Any_Padro
WHERE Habitants=minHabitants and Padro=Any_Padro
RETURN Any_Padro, Carrer, Habitants
ORDER BY Any_Padro
